
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ,Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания >= ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала
		|			ТОГДА СУММА(РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала,
		|				ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, ДЕНЬ) + 1)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДнейОтпуска,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.НомерСтроки КАК НомерСтроки,
		|	ВКМ_МаксимальноеЧислоДнейОтпуска.Значение КАК МаксимумДнейОтпуска,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания
		|ИЗ
		|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников,
		|	Константа.ВКМ_МаксимальноеЧислоДнейОтпуска КАК ВКМ_МаксимальноеЧислоДнейОтпуска
		|ГДЕ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.НомерСтроки,
		|	ВКМ_МаксимальноеЧислоДнейОтпуска.Значение
		|ИТОГИ
		|	СУММА(ДнейОтпуска),
		|	МИНИМУМ(НомерСтроки),
		|	МИНИМУМ(МаксимумДнейОтпуска)
		|ПО
		|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаСотрудник = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	// регистр ВКМ_ГрафикОтпусков
	Движения.ВКМ_ГрафикОтпусков.Записывать = Истина;
	Пока ВыборкаСотрудник.Следующий() Цикл
		Движение = Движения.ВКМ_ГрафикОтпусков.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Сотрудник = ВыборкаСотрудник.Сотрудник;
		Движение.ДнейОтпуска = ВыборкаСотрудник.ДнейОтпуска;
		Если ВыборкаСотрудник.ДнейОтпуска > ВыборкаСотрудник.МаксимумДнейОтпуска Тогда
			ТекстСообщения = СтрШаблон("Превышение общего числа дней отпуска максимального значения у сотрудника ""%1""", ВыборкаСотрудник.Сотрудник);
			ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОтпускаСотрудников", ВыборкаСотрудник.НомерСтроки, "ДнейОтпуска");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПутьКТЧ, "Объект");	
			Отказ = Истина;
		КонецЕсли;
		Выборка = ВыборкаСотрудник.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ДатаНачала > Выборка.ДатаОкончания Тогда
				ТекстСообщения = СтрШаблон("Неверно задан период отпуска у сотрудника ""%1""", Выборка.Сотрудник);
				ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОтпускаСотрудников", Выборка.НомерСтроки, "ДатаОкончания");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПутьКТЧ, "Объект");	
				Отказ = Истина;
			КонецЕсли;
			
		КонецЦикла;

	КонецЦикла;
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

#КонецОбласти

#КонецЕсли